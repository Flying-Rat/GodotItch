#!/usr/bin/env python
import os
import sys

env = SConscript("godot-cpp/SConstruct")

# For reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "demo/bin/libgodotitch.{}.{}.framework/libgodotitch.{}.{}".format(
            env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
elif env["platform"] == "ios":
    if env["ios_simulator"]:
        library = env.StaticLibrary(
            "demo/bin/libgodotitch.{}.{}.simulator.a".format(env["platform"], env["target"]),
            source=sources,
        )
    else:
        library = env.StaticLibrary(
            "demo/bin/libgodotitch.{}.{}.a".format(env["platform"], env["target"]),
            source=sources,
        )
else:
    library = env.SharedLibrary(
        "demo/bin/libgodotitch{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

# Install/copy build artifacts (DLLs/libs) to the addon gdextension folder
install_dir = os.path.join("addons", "godot_itch", "gdextension")
# Always install the produced library
install_targets = env.Install(install_dir, library)

# Additionally, copy common runtime/link artifacts if they exist
extra_artifacts = []
# Shared libs from demo/bin (e.g., .dll, .so, .dylib)
extra_artifacts += Glob("demo/bin/*{}".format(env.get("SHLIBSUFFIX", "")))
# Windows import/static libs
if env.get("platform") == "windows":
    extra_artifacts += Glob("demo/bin/*.lib")
# If godot-cpp produced runtime/link files, copy them too (optional)
extra_artifacts += Glob("godot-cpp/bin/*{}".format(env.get("SHLIBSUFFIX", "")))
if env.get("platform") == "windows":
    extra_artifacts += Glob("godot-cpp/bin/*.lib")

if extra_artifacts:
    install_targets += env.Install(install_dir, extra_artifacts)

Default([library, install_targets])
